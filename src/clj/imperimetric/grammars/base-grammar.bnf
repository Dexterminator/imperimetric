text = token+
<token> = combined / measurement-expr / interval / word / whitespace
interval = quantity-no-numeral dash quantity-no-numeral <whitespace>* unit
<dash> = '-'
<word> = #'[^\s]+'
<measurement-expr> = common-prefix-char? measurement / common-prefix-char? allowed-distance-prefix? distance-expr
measurement = quantity <whitespace>* unit <#'(?!\w)'> / maybe-negative-quantity <whitespace>* temperature <#'(?!\w)'>
<distance-expr> = distance-measurement allowed-distance-suffix?
distance-measurement = quantity <whitespace*> distance
<allowed-distance-prefix> = allowed-distance-suffix
<allowed-distance-suffix> = 'x' | 'X' | 'L' | 'W' | 'H' | 'D'
<common-prefix-char> = '(' | '"' | '\''
<whitespace> = #'\s+'
<maybe-negative-quantity> = negative-quantity | quantity
negative-quantity = <minus> quantity
<minus> = '-'
<quantity> = quantity-no-numeral | numeral
<quantity-no-numeral> = mixed | integer | decimal | fraction
mixed = integer <whitespace> fraction
integer = #'\d+'
decimal = #'\d+\.\d+' | implicit-zero-decimal
implicit-zero-decimal = #'\.\d+'
fraction = #'[1-9]+\/[1-9]+' | unicode-fraction
<numeral> = 1-9 | 10-19 | 20-99
unicode-fraction = '¼' | '½' | '¾' | '⅓' | '⅔' | '⅕' | '⅖'
1-9 = #'(?i)one' | #'(?i)two' | #'(?i)three' | #'(?i)four' |
#'(?i)five' | #'(?i)six' | #'(?i)seven' | #'(?i)eight' | #'(?i)nine'
10-19 = #'(?i)ten' | #'(?i)eleven' | #'(?i)twelve' | #'(?i)thirteen' |
#'(?i)fourteen' | #'(?i)fifteen' | #'(?i)sixteen' | #'(?i)seventeen' |
#'(?i)eighteen' | #'(?i)nineteen'
<20-99> = base | base-with-suffix
base = #'(?i)twenty' | #'(?i)thirty' | #'(?i)forty' | #'(?i)fifty' |
#'(?i)sixty' | #'(?i)seventy' | #'(?i)eighty' | #'(?i)ninety'
base-with-suffix = base <'-'?> 1-9
square-prefix = #'(?i)square' | #'(?i)sq'
square-suffix = '^2'| '²' | '2'
