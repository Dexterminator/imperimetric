unit = floz | cup | tablespoon | teaspoon | gallon | pint | quart | gill | mile | yard | foot | inch |
pound | oz | ton | sqmile | sqyard | sqfoot | sqinch | acre | mph | fps | fahrenheit

(* Volume *)
floz = #'(?i)fl\.? ozs?' | #'(?i)fl\.?ozs?' | #'(?i)fluid ounces?'
cup = #'(?i)cups?' | #'(?i)cp'
tablespoon = #'(?i)tbsps?' | #'(?i)tablespoons?'
teaspoon = #'(?i)tsps?' | #'(?i)teaspoons?'
gallon = #'(?i)gallons?' | #'(?i)gals?'
pint = #'(?i)pints?' | #'(?i)pts?'
quart = #'(?i)quarts?' | #'(?i)qts?'
gill = #'(?i)gills?'

(* Distance *)
mile = #'(?i)miles?' | #'(?i)mi'
yard = #'(?i)yards?' | #'(?i)yd'
foot = #'(?i)foot' | #'(?i)feet' | #'(?i)ft' | '\''
inch = #'(?i)inch(es)?' | #'(?i)in' | '\"'

(* Weight *)
pound = #'(?i)pounds?' | #'(?i)lbs?'
oz = #'(?i)ozs?' | #'(?i)ounces?'
ton = #'(?i)tons?'

(* Area *)
sqmile = square-prefix <' '> mile | mile square-suffix
sqyard = square-prefix <' '> yard | yard square-suffix
sqfoot = square-prefix <' '> foot | foot square-suffix
sqinch = square-prefix <' '> inch | inch square-suffix
acre = #'(?i)acres?' | #'(?i)ac'

(* Speed *)
mph = #'(?i)miles per hour'|#'(?i)mph'|'mi/h'
fps = #'(?i)feet per second'|#'(?i)fps'|'ft/s'

(* Temperature *)
temperature = fahrenheit
fahrenheit = degree-prefix <' '>? degree-suffix | degree-suffix
<degree-prefix> = #'(?i)degrees?' | #'(?i)deg' | 'Â°'
<degree-suffix> = #'(?i)fahrenheit' | #'(?i)F'

(* Combined *)
combined = pounds-ounces | mile-lower | yard-lower | feet-inches
<mile-lower> = quantity <' '>? mile <' '>? quantity <' '>? lower-than-mile
<lower-than-mile> = yard | foot | inch
<yard-lower> = quantity <' '>? yard <' '>? quantity <' '>? lower-than-yard
<lower-than-yard> = foot | inch
<pounds-ounces> = quantity <' '>? pound <' '>? quantity <' '>? oz
<feet-inches> = quantity <' '>? foot <' '>? quantity <' '>? inch
